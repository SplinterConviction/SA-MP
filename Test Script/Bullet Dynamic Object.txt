//StreamerFunction.inc

#if defined ENABLE_BULLET_DYNAMIC_OBJECT

	//3DTryg Functions
	stock Float:SSpec_CompRotationFloat(Float:rotation,&Float:crotation=0.0){
		crotation = rotation;
		while(crotation < 0.0) crotation += 360.0;
		while(crotation >= 360.0) crotation -= 360.0;
		return crotation;
	}
	
	stock SSpec_OffsetToCoordinates(Float:offsetx,Float:offsety,Float:offsetz,Float:x,Float:y,Float:z,Float:rx,Float:ry,Float:rz,&Float:tx,&Float:ty,&Float:tz){
		//ry ?
		new Float:radius = VectorSize(offsetx,offsety,offsetz);
		if(radius > 0.0){
			SSpec_CompRotationFloat(-(acos((offsetz)/radius)-90.0+rx),rx);
			SSpec_CompRotationFloat((atan2(offsety,offsetx)-90.0+rz),rz);
			tx = x + (radius * floatsin(rx+90.0,degrees) * floatcos(rz+90.0,degrees));
			ty = y + (radius * floatsin(rx+90.0,degrees) * floatsin(rz+90.0,degrees));
			tz = z + (radius * floatcos(-rx+90.0,degrees));
		} else {
			tx = x;
			ty = y;
			tz = z;
		}
	}

	//Hook: OnPlayerShootDynamicObject
	public OnPlayerShootDynamicObject(playerid,weaponid,objectid,Float:x,Float:y,Float:z){
		
		new Float:ox, Float:oy, Float:oz, Float:tx, Float:ty, Float:tz, Float:rx, Float:ry, Float:rz;
		GetDynamicObjectPos(objectid,ox,oy,oz);
		GetDynamicObjectRot(objectid,rx,ry,rz);
		SSpec_OffsetToCoordinates(x,y,z,ox,oy,oz,rx,ry,rz,tx,ty,tz);
		//printf("A: %d %d %d %f %f %f",playerid,weaponid,objectid,tx,ty,tz);
		
		#if defined OnPlayerWeaponShot
			OnPlayerWeaponShot(playerid,weaponid,BULLET_HIT_TYPE_DYNAMIC_OBJECT,0,tx,ty,tz);
		#endif
		#if defined SSpec_OnShootDynamicObject
			SSpec_OnShootDynamicObject(playerid,weaponid,objectid,x,y,z);
		#endif
		return 1;
	}

	#if defined _ALS_OnPlayerShootDynamicObject
		#undef OnPlayerShootDynamicObject
	#else
		#define _ALS_OnPlayerShootDynamicObject
	#endif
	#define OnPlayerShootDynamicObject SSpec_OnShootDynamicObject
	#if defined SSpec_OnShootDynamicObject
		forward SSpec_OnShootDynamicObject(playerid,weaponid,objectid,Float:x,Float:y,Float:z);
	#endif
	
#endif
